apply plugin: 'com.android.application'

def ADB_PATH = "${android.getSdkDirectory()}/platform-tools/adb"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.yyxx.wechatfp"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 6
        versionName "1.3.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    repositories{
        flatDir{
            dirs 'libs'
        }
    }
}


android.applicationVariants.all { variant ->
    variant.getCompileConfiguration().resolutionStrategy {
        if (variant.install != null) {
            variant.install.doLast {
                println "Running app..."
                exec {
                    executable = ADB_PATH
                    args = ['shell', "am start -n ${variant.applicationId}/.SettingsActivity"]
                }
            }
        }
    }
}

task debugWechat(dependsOn: "installDebug") {
    doLast {
        println "Stop WeChat..."
        exec {
            executable = ADB_PATH
            args = ['shell', "am force-stop com.tencent.mm"]
        }
        println "Start WeChat..."
        exec {
            executable = ADB_PATH
            args = ['shell', "am start -n com.tencent.mm/com.tencent.mm.ui.LauncherUI"]
        }

    }
}


dependencies {
    implementation 'com.android.support:support-v4:26.0.2'
    implementation 'com.wei.android.lib:fingerprintidentify:1.2.1'
    implementation 'com.crossbowffs.remotepreferences:remotepreferences:0.5'
    provided 'de.robv.android.xposed:api:53'
}